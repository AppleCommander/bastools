plugins {
    id 'org.springframework.boot' version "2.7.18"
    id 'java'
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.10.6'
}

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Shape Tools CLI',
                'Implementation-Version': "${project.version} (${new Date().format('yyyy-MM-dd HH:mm')})"
        )
    }
}

application {
    mainClass = "io.github.applecommander.bastools.tools.st.Main"
}

bootJar {
  manifest {
    attributes(
      'Implementation-Title': 'Shape Tools CLI',
      'Implementation-Version': "${project.version} (${new Date().format('yyyy-MM-dd HH:mm')})"
    )
  }
}

dependencies {
    implementation 'info.picocli:picocli:4.7.7'
    implementation 'net.sf.applecommander:applesingle-api:1.2.2'
    implementation 'net.sf.applecommander:AppleCommander:1.10.1'
    implementation project(':bastools-api')
}

graalvmNative {
    String osName = System.getProperty('os.name').toLowerCase().split()[0]
    String osArch = System.getProperty('os.arch').toLowerCase().split()[0]
    binaries {
        named("main") {
            imageName.set("st-${osName}-${osArch}-${project.version}")
        }
    }
}
